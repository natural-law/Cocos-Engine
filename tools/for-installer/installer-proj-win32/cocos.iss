; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppName "cocos"
#define MyAppVersion "3.1"
#define MyAppPublisher "Chukong-inc"
#define MyAppURL "http://www.cocos2d-x.org/"
#define MyExeName "Cocos Code IDE"
#define MyAppExeName "IDE\Cocos Code IDE.exe"
#define MyRootDir "..\..\.."
#define MyInstallGroupName "cocos"

#define StudioInstaller "CocosStudio.exe"
#define StudioSetupINI "studiosetup.ini"
#define RunFirstBat "cocos_runfirst.bat"

#define IDEPath "IDE\Cocos Code IDE.exe"
#define IDEExeName "Cocos Code IDE.exe"
#define AnySDKPath "AnySDK\bin\AnySDK.exe"
#define AnySDKExeName "AnySDK.exe"

#define PythonDir "tools\Python27"

#define JDKInstaller "jdk.exe"
#define NDKFolderName "android-ndk-r9d"

#define JavaHome "C:\Program Files\Java\jdk1.6.0_45"

#if BitFlag == "64bit"
#define DefaultDir "{pf64}"
#else
#define DefaultDir "{pf32}"
#endif

[Setup]
; NOTE: The value of AppId uniquely identifies this application.
; Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{25521E37-D0A4-4510-8400-5E1933112AA1}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
;AppVerName={#MyAppName} {#MyAppVersion}
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}
DefaultGroupName={#MyInstallGroupName}
DefaultDirName={#DefaultDir}\{#MyInstallGroupName}
AllowNoIcons=yes
LicenseFile=..\resources-common\license.txt
OutputDir={#MyRootDir}\release
OutputBaseFilename={#MyAppName}-{#BitFlag}
Compression=lzma
SolidCompression=yes
SetupIconFile=resources\Icon.ico
WizardImageFile=resources\wizard.bmp
WizardSmallImageFile=resources\smallicon.bmp

[Types]
Name: "full"; Description: "Full installation"
Name: "compact"; Description: "Compact installation"
Name: "custom"; Description: "Custom installation"; Flags: iscustom

[Components]
Name: "creator"; Description: "Applications & prebuilt engine"; Types: full compact custom; Flags: fixed
Name: "sourcecode"; Description: "Source code of engine";  Types: full

[Files]
Source: "resources\unzip.exe"; DestDir: {tmp}; Flags: deleteafterinstall; Components: creator
Source: "{#MyRootDir}\gen\cocos\frameworks\*"; DestDir: "{app}\frameworks"; Flags: recursesubdirs; Components: creator
Source: "{#MyRootDir}\gen\cocos\tools\ant\*"; DestDir: "{app}\tools\ant"; Flags: recursesubdirs; Components: creator
Source: "{#MyRootDir}\gen-win32\android-sdk\*"; DestDir: "{app}\tools\android-sdk"; Components: creator
Source: "{#MyRootDir}\gen-win32\{#BitFlag}\*"; DestDir: "{app}"; Excludes: "{#JDKInstaller},{#NDKFolderName}.zip"; Flags: recursesubdirs; Components: creator

Source: "resources\{#RunFirstBat}"; DestDir: "{tmp}"; Components: creator
Source: "{#MyRootDir}\gen-win32\{#StudioInstaller}"; DestDir: "{tmp}"; Flags: nocompression deleteafterinstall; Components: creator
Source: "{#MyRootDir}\gen-win32\{#BitFlag}\{#JDKInstaller}"; DestDir: "{tmp}"; Flags: nocompression deleteafterinstall; Components: creator
Source: "{#MyRootDir}\gen-win32\{#BitFlag}\{#NDKFolderName}.zip"; DestDir: "{tmp}"; Flags: nocompression deleteafterinstall; Components: creator

Source: "{#MyRootDir}\gen-src\cocos\*"; DestDir: "{app}"; Flags: recursesubdirs; Components: sourcecode

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"

;[Messages]
;WelcomeLabel1=Welcome to the [name] Setup Wizard
;WelcomeLabel2=This will install [name/ver] on your computer.%n%n

[Tasks]
Name: "desktopicon"; Description: "{cm:CreateDesktopIcon}"; GroupDescription: "{cm:AdditionalIcons}"
Name: "quicklaunchicon"; Description: "{cm:CreateQuickLaunchIcon}"; GroupDescription: "{cm:AdditionalIcons}"; Flags: unchecked; OnlyBelowVersion: 0,6.1

[Icons]
Name: "{group}\{#IDEExeName}"; Filename: "{app}\{#IDEPath}"
Name: "{group}\{#AnySDKExeName}"; Filename: "{app}\{#AnySDKPath}"
Name: "{group}\{cm:ProgramOnTheWeb,{#MyAppName}}"; Filename: "{#MyAppURL}"
Name: "{group}\{cm:UninstallProgram,{#MyAppName}}"; Filename: "{uninstallexe}"
Name: "{commondesktop}\Cocos Code IDE.exe"; Filename: "{app}\{#IDEPath}"; Tasks: desktopicon
Name: "{commondesktop}\AnySDK.exe"; Filename: "{app}\{#AnySDKPath}"; Tasks: desktopicon

[Registry]
Root: HKLM; Subkey: "SYSTEM\CurrentControlSet\Control\Session Manager\Environment"; ValueType: expandsz; ValueName: "Path"; ValueData: "{olddata};{app}\{#PythonDir}"; Check: NeedsAddPath('{app}\{#PythonDir}')
Root: HKLM; Subkey: "SYSTEM\CurrentControlSet\Control\Session Manager\Environment"; ValueType: expandsz; ValueName: "JAVA_HOME"; ValueData: "{#JavaHome}"
Root: HKLM; Subkey: "SYSTEM\CurrentControlSet\Control\Session Manager\Environment"; ValueType: expandsz; ValueName: "Path"; ValueData: "{olddata};%JAVA_HOME%/bin"; Check: NeedsAddPath('%JAVA_HOME%/bin')
Root: HKLM; Subkey: "SYSTEM\CurrentControlSet\Control\Session Manager\Environment"; ValueType: expandsz; ValueName: "Path"; ValueData: "{olddata};%JAVA_HOME%/jre/bin"; Check: NeedsAddPath('%JAVA_HOME%/jre/bin')

[Run]
Filename: "{tmp}\unzip.exe"; Parameters: """{tmp}\{#NDKFolderName}.zip"" -d ""{app}\tools"""; StatusMsg: "Extracting android NDK..."
Filename: "{tmp}\{#RunFirstBat}"; Parameters: """{tmp}\{#StudioSetupINI}"" ""{app}\Cocos Studio"""; StatusMsg: "Writing configuration..."
Filename: "{tmp}\{#StudioInstaller}"; Parameters: """/S:{tmp}\{#StudioSetupINI}"" /NOINIT"; StatusMsg: "Installing Cocos Studio..."
Filename: "{tmp}\{#JDKInstaller}"; Parameters: "/s"; StatusMsg: "Installing JDK..."
Filename: "{app}\{#PythonDir}\python.exe"; Parameters: """{app}\frameworks\cocos2d-x\setup.py"" -a ""{app}\tools\android-sdk"" -n ""{app}\tools\android-ndk-r9d"" -t ""{app}\tools\ant\bin"""
Filename: "{app}\{#MyAppExeName}"; Description: "{cm:LaunchProgram,{#StringChange(MyExeName, '&', '&&')}}"; Flags: nowait postinstall skipifsilent

[UninstallRun]
Filename: "{app}\Cocos Studio\InstallData\uninstall.exe"; Parameters: """/U:{app}\Cocos Studio\InstallData\uninstall.xml"" /S"; Flags: skipifdoesntexist

[UninstallDelete]
Type: filesandordirs; Name: "{app}\Cocos Studio"
Type: filesandordirs; Name: "{app}\tools"

[Code]
function NeedsAddPath(Param: string): boolean;
var
  OrigPath: string;
begin
  if not RegQueryStringValue(HKEY_LOCAL_MACHINE,
    'SYSTEM\CurrentControlSet\Control\Session Manager\Environment',
    'Path', OrigPath)
  then begin
    Result := True;
    exit;
  end;
  // look for the path with leading and trailing semicolon
  // Pos() returns 0 if not found
  Result := Pos(';' + Param + ';', ';' + OrigPath + ';') = 0;
end;

